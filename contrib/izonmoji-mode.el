;;; izonmoji-mode.el --- Visualize Windows and Macintosh izonmoji

;; Copyright (C) 2002 by Navi2ch Project

;; Author: SAITO Takuya <tabmore@users.sourceforge.net>
;; Keywords: 2ch, charset

;; This file is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 2, or (at your option)
;; any later version.

;; This file is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with GNU Emacs; see the file COPYING.  If not, write to
;; the Free Software Foundation, Inc., 59 Temple Place - Suite 330,
;; Boston, MA 02111-1307, USA.

;;; Commentary:

;; 全ての機種依存文字を表示できるわけではありませんが、
;; 最低限必要なものは揃えたつもりです。

;; commands:
;;   izonmoji-mode	機種依存文字表示をトグル
;;   izonmoji-mode-on	機種依存文字を表示
;;   izonmoji-mode-off	機種依存文字表示をやめる

;; GNU Emacs 20 では、Mule-UCSが必要です。
;; このファイルを読み込む前に (require 'jisx0213) してください。

;; GNU Emacs 20,21では、buffer-display-tableによって表示を置き替えた
;; Non-ASCIIな文字のchar-widthがおかしくなります。
;; 変換前後でchar-widthが変わらない場合は、
;;  (defadvice char-width (around display-table-hack activate)
;;    (let ((buffer-display-table nil))
;;      ad-do-it))
;; でごまかせます。 string-widthも同様です。

;; XEmacsではinit-fileに以下のように書いてください。
;; (make-charset
;;  'japanese-jisx0213-1
;;  "JIS X 0213:2000 Plain 1"
;;  '(registry "jisx0213\\(\\.2000\\)-1"
;;             dimension 2 chars 94 final ?O graphic 0))
;; (make-charset
;;  'japanese-jisx0213-2
;;  "JIS X 0213:2000 Plain 2"
;;  '(registry "jisx0213\\(\\.2000\\)-2"
;;             dimension 2 chars 94 final ?P graphic 0))

;; 設定例

;; [共通] ~/.emacs へ
;;  (require 'izonmoji-mode)

;; [navi2ch] ~/.navi2ch/init.el へ
;;  (add-hook 'navi2ch-bm-mode-hook      'izonmoji-mode-on)
;;  (add-hook 'navi2ch-article-mode-hook 'izonmoji-mode-on)

;; [Mew] ~/.mew.el へ
;;  (add-hook 'mew-message-mode-hook 'izonmoji-mode-on)

;; [emacs-w3m] ~/.emacs-w3m.el へ
;;  (add-hook 'w3m-mode-hook 'izonmoji-mode-on)

;;; Bugs:

;;  1. display-tableをいじる
;;  2. M-x izonmoji-mode-on
;;  3. 1の変更を元に戻す
;;  4. M-x izonmoji-mode-off
;;  すると元にもどらない。
;;  C-u M-x izonmoji-mode-off してdisplay-tableへの全ての変更を取り消す
;;  ことはできます。

;; XEmacs
;;  izonmoji-win-face, izonmoji-mac-face の適用方法を知らない。

;;; Code:

(defvar izonmoji-priority-list '(win mac)
  "*表示の優先順位。
'(win mac) なら、Windowsの機種依存文字を優先しつつ、Macの文字も表示。
'(win) なら、Windowsの機種依存文字のみ表示。")

(defvar izonmoji-win-replace-list
  '("-!" "-"" "-#" "-$" "-%" "-&" "-'" "-(" "-)" "-*"
    "-+" "-," "--" "-." "-/" "-0" "-1" "-2" "-3" "-4"
    "-5" "-6" "-7" "-8" "-9" "-:" "-;" "-<" "-=" "->"
    ",5" ",6" ",7" ",8" ",9" ",:" ",;" ",<" ",=" ",>"
    "-@" "-A" "-B" "-C" "-D" "-E" "-F" "-G" "-H" "-I" "-J" "-K"
    "-L" "-M" "-N" "-O" "-P" "-Q" "-R" "-S" "-T" "-U" "-V"
    "-_" "-`" "-a" "-b" "-c" "-d" "-e" "-f" "-g" "-h" "-i"
    "-j" "-k" "-l" "-m" "-n" "-o"
    "≒" "≡" "∫" "-s" "Σ" "√" "⊥" "∠" "-x" "-y" "∵" "∩" "∪")
  "*Strings used to visualize Windows izonmoji.")

(defvar izonmoji-mac-replace-list
  '("-!" "-"" "-#" "-$" "-%" "-&" "-'" "-(" "-)" "-*"
    "-+" "-," "--" "-." "-/" "-0" "-1" "-2" "-3" "-4"
    "(1)" "(2)" "(3)" "(4)" "(5)" "(6)" "(7)" "(8)" "(9)" "(10)"
    "(11)" "(12)" "(13)" "(14)" "(15)" "(16)" "(17)" "(18)" "(19)" "(20)"
    ",!" ","" ",#" ",$" ",%" ",&" ",'" ",(" ",)"
    "1." "2." "3." "4." "5." "6." "7." "8." "9." "0."
    "-5" "-6" "-7" "-8" "-9" "-:" "-;" "-<" "-=" "->"
    "-?" "-W" "X-7" "X-8" "XV"
    ",5" ",6" ",7" ",8" ",9" ",:" ",;" ",<" ",=" ",>"
    ",?" ",@" "x,7" "x,8" "xv"
    "(a)" "(b)" "(c)" "(d)" "(e)" "(f)" "(g)" "(h)" "(i)" "(j)" "(k)"
    "(l)" "(m)" "(n)" "(o)" "(p)" "(q)" "(r)" "(s)" "(t)" "(u)" "(v)"
    "(w)" "(x)" "(y)" "(z)"
    "-P" "mm)," "-Q" "-Q)," "-Q)-" "m" "-V" "m)-" "-R" "k-V"
    "-S" "g" "-T" "-U" "m#_" "d#_" "#_" "k#_"
    "ms" "μs" "ns" "ps" "!kF" "mb" "HP" "Hz" "KB" "MB" "GB" "TB"
    "-b" "-c" "-d" "FAX"
    "&9" "&?" "&=" "&;" "&:" "&@" "&>" "&<"
    "&f" "&g" "JIS"			;JISマーク
    "→" "←" "↑" "↓"			;全然違う
    "#)" "#)" "↑↓" "↓↑"		;ちょっと違う
    "#*" "#+" "#," "#-" "#*" "#+" "#," "#-" ;後半は塗り潰し
    "(日)" "(月)" "(火)" "(水)" "(木)" "(金)" "(土)"
    "(祭)" "(祝)" "(自)" "(至)" "-l" "(呼)" "-j" "(資)" "(名)" 
    "-k" "(学)" "(財)" "(社)" "(特)" "(監)" "(企)" "(協)" "(労)"
    "(大)" "(小)" "-e" "-f" "-g" "-h" "-i"
    "(医)" "(財)" "(優)" "(労)" "(印)" "(控)" "(秘)" ;本当は丸付き
    "-@" "-B" "-C" "-A" "-A-C" "2]A" "L(0D" "T0D^" "-F" "-G"
    "-D" "-A-D" "-E" "-H" "-N" "MYB" "-I" "-J" "N0]" "-L" "-K" "-O" "-M"
    "1J_0D" ":0N_" "J2B" "K^Y" "O]<.]"
    "-m" "-n" "-o" "-_"
    "株式会社" "有限会社" "財団法人"	;"-j" "-k"
    "-s" "-x" "-y"
    "-`" "-a"
    "$t" "'r" "'s" "'t" "'u")
  "*Strings used to visualize Macintosh izonmoji.")

(defvar izonmoji-win-face 'izonmoji-win-face
  "*Symbol face used to visualize Windows izonmoji.")

(defvar izonmoji-mac-face 'izonmoji-mac-face
  "*Symbol face used to visualize Macintosh izonmoji.")

(defface izonmoji-win-face
  '((((class color) (type tty)) (:foreground "cyan"))
    (((class color) (background light)) (:foreground "Aquamarine4"))
    (((class color) (background dark))  (:foreground "Aquamarine3"))
    (t (:underline t)))
  "Face used to visualize Windows izonmoji.")

(defface izonmoji-mac-face
  '((((class color) (type tty)) (:foreground "magenta"))
    (((class color) (background light)) (:foreground "pink4"))
    (((class color) (background dark))  (:foreground "pink3"))
    (t (:underline t)))
  "Face used to visualize Macintosh izonmoji.")

(defun izonmoji-make-char-list (i js je)
  (let ((j js) list)
    (while (<= j je)
      (setq list (cons (make-char 'japanese-jisx0208 i j) list))
      (setq j (1+ j)))
    (nreverse list)))

;; Windowsの丸付き1は、
;; (split-char (decode-sjis-char (hexl-hex-string-to-integer "8740")))
(defvar izonmoji-win-chars-list
  (append
   (izonmoji-make-char-list  45  33  52) ;丸付き数字
   (izonmoji-make-char-list  45  53  62) ;ローマ数字(大文字)
   (izonmoji-make-char-list 124 113 122) ;ローマ数字(小文字)
   (izonmoji-make-char-list  45  64  86) ;単位
   (izonmoji-make-char-list  45  95 111) ;元号など
   (izonmoji-make-char-list  45 112 124) ;数学記号
   )
  "*Windows izonmoji.")

(defvar izonmoji-mac-chars-list
  (append
   (izonmoji-make-char-list  41  33  52) ;丸付き数字
   (izonmoji-make-char-list  41  63  82) ;括弧付き数字
   (izonmoji-make-char-list  41  93 101) ;黒丸付き数字
   (izonmoji-make-char-list  41 114 123) ;点付き数字
   (izonmoji-make-char-list  42  33  47) ;ローマ数字(大文字)
   (izonmoji-make-char-list  42  53  67) ;ローマ数字(小文字)
   (izonmoji-make-char-list  42  93 118) ;括弧付きアルファベット
   (izonmoji-make-char-list  43  33  62) ;単位
   (izonmoji-make-char-list  43 123 126) ;略号
   (izonmoji-make-char-list  44  33  40) ;トランプ
   (izonmoji-make-char-list  44  53  55) ;郵便
   (izonmoji-make-char-list  44  73  88) ;矢印
   (izonmoji-make-char-list  45  33  57) ;曜日など
   (izonmoji-make-char-list  45 113 126) ;丸付き文字
   (izonmoji-make-char-list  46  33  55) ;カタカナ単位
   (izonmoji-make-char-list  46  63  67) ;アパート
   (izonmoji-make-char-list  46 103 106) ;元号
   (izonmoji-make-char-list  46 124 126) ;株式会社
   (izonmoji-make-char-list  47  33  35) ;数学記号
   (izonmoji-make-char-list  47  53  54) ;""
   (izonmoji-make-char-list  47  73  73) ;う゛
   (izonmoji-make-char-list  47  75  78) ;ワ゛
   )
  "*Macintosh izonmoji.")

(defvar izonmoji-mode-hook nil
  "*Hook run after izonmoji visualization.")

;; Internal variables

(defvar izonmoji-mode nil)
(make-variable-buffer-local 'izonmoji-mode)

(defvar izonmoji-backuped-display-table nil)
(make-variable-buffer-local 'izonmoji-backuped-display-table)

(defun izonmoji-mode (&optional arg)
  "Toggle izonmoji visualization.
If ARG is nil, toggle izonmoji visualization.
If ARG is a number and is greater than zero, turn on visualization; otherwise,
turn off visualization."
  (interactive "P")
  (if (if arg
	  (> (prefix-numeric-value arg) 0)
	(not izonmoji-mode))
      (izonmoji-mode-on)
    (izonmoji-mode-off)))

(defun izonmoji-mode-on (&optional reverse win-face mac-face)
  "Turn on visualization."
  (interactive "P")
  (let ((priority (reverse izonmoji-priority-list))
	from to table face-bits)
    (when reverse
      (setq priority (nreverse priority)))
    (unless izonmoji-mode
      (cond
       ((featurep 'xemacs)
	(let* ((ctable (specifier-instance current-display-table))
	       (len (- (1+ (apply 'max (append izonmoji-win-chars-list
					       izonmoji-mac-chars-list)))
		       (length ctable))))
	  (setq izonmoji-backuped-display-table (copy-sequence ctable))
	  (if (> len 0)
	      (setq table (vconcat ctable (make-vector len nil)))
	    (setq table ctable)))
	(while priority
	  (cond
	   ((eq (car priority) 'win)
	    (setq from izonmoji-win-chars-list
		  to   izonmoji-win-replace-list))
	   ((eq (car priority) 'mac)
	    (setq from izonmoji-mac-chars-list
		  to   izonmoji-mac-replace-list)))
	  (setq priority (cdr priority))
	  (while (and from to)
	    (aset table (car from) (car to))
	    (setq from (cdr from) to (cdr to))))
	(set-specifier current-display-table table (current-buffer)))
       (t				;GNU Emacs
	(setq izonmoji-backuped-display-table
	      (copy-sequence buffer-display-table)
	      table (or buffer-display-table (make-display-table)))
	(while priority
	  (cond
	   ((eq (car priority) 'win)
	    (setq from izonmoji-win-chars-list
		  to   izonmoji-win-replace-list
		  face-bits (ash (face-id (or win-face izonmoji-win-face))
				 19)))
	   ((eq (car priority) 'mac)
	    (setq from izonmoji-mac-chars-list
		  to   izonmoji-mac-replace-list
		  face-bits (ash (face-id (or mac-face izonmoji-mac-face))
				 19))))
	  (setq priority (cdr priority))
	  (while (and from to)
	    (aset table (car from)
		  (apply 'vector (mapcar
				  (lambda (ch) (logior ch face-bits))
				  (car to))))
	    (setq from (cdr from) to (cdr to))))
	(setq buffer-display-table table))))
    (setq izonmoji-mode t)
    (run-hooks 'izonmoji-mode-hook)))

(defun izonmoji-mode-off (&optional initialize)
  "Turn on visualization."
  (interactive "P")
  (when initialize
    (setq izonmoji-mode t
	  izonmoji-backuped-display-table (make-display-table)))
  (when izonmoji-mode
    (if (featurep 'xemacs)
	(set-specifier current-display-table
		       izonmoji-backuped-display-table (current-buffer))
      (setq buffer-display-table izonmoji-backuped-display-table))
    (setq izonmoji-mode nil)))

(add-to-list 'minor-mode-alist '(izonmoji-mode " Iz"))

(provide 'izonmoji-mode)

;;; izonmoji-mode.el ends here
